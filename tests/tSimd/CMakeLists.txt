function(add_extra_test_compile_and_link_options TARGET_NAME)
    if(MSVC)
        if(TMATH_TEST_OPTION STREQUAL "od")
            target_compile_options(${TARGET_NAME} PRIVATE /Od) # Debug: Od
        endif()
        if(TMATH_TEST_OPTION STREQUAL "o2")
            target_compile_options(${TARGET_NAME} PRIVATE /O2) # Release: O2
        endif()
        if(TMATH_TEST_OPTION STREQUAL "gl")
            target_compile_options(${TARGET_NAME} PRIVATE /O2 /GL) # Release: O2 GL
            target_link_options(${TARGET_NAME} PRIVATE /LTCG)
        endif()
    else()
        if(TMATH_TEST_OPTION STREQUAL "od")
            target_compile_options(${TARGET_NAME} PRIVATE -O0) # Debug: O0
        endif()
        if(TMATH_TEST_OPTION STREQUAL "o2")
            target_compile_options(${TARGET_NAME} PRIVATE -O2) # Release: O2
        endif()
        if(TMATH_TEST_OPTION STREQUAL "gl")
            target_compile_options(${TARGET_NAME} PRIVATE -O2 -flto) # Release: O2 -flto
            target_link_options(${TARGET_NAME} PRIVATE -flto=auto)
        endif()
    endif()
endfunction()

function(add_tsimd_test_target TARGET_NAME SOURCE_FILE)
    set(FINAL_TARGET_NAME ${TARGET_NAME}_${TMATH_TEST_OPTION})
    add_executable(${FINAL_TARGET_NAME} ${SOURCE_FILE})
    if(MSVC)
        target_compile_options(${FINAL_TARGET_NAME} PRIVATE /utf-8 /arch:AVX2 /arch:AVX) # /arch:SSE4.2 /arch:SSE2 现在对MSVC无效
        target_compile_definitions(${FINAL_TARGET_NAME} PRIVATE UNICODE _UNICODE)
    else()
        target_compile_options(${FINAL_TARGET_NAME} PRIVATE -mavx2 -mavx -mfma -mf16c -msse4.1 -msse3 -msse2)
    endif()
    add_extra_test_compile_and_link_options(${FINAL_TARGET_NAME})

    target_compile_definitions(${FINAL_TARGET_NAME} PRIVATE TSIMD_IS_TESTING)
    target_link_libraries(${FINAL_TARGET_NAME} PRIVATE gtest tSimd)

    get_target_property(OPTIONS ${FINAL_TARGET_NAME} COMPILE_OPTIONS)
    message(STATUS "${FINAL_TARGET_NAME} compile options: ${OPTIONS}")

    add_test(NAME ${FINAL_TARGET_NAME} COMMAND $<TARGET_FILE:${FINAL_TARGET_NAME}>)
endfunction()

file(GLOB_RECURSE TEST_SOURCES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "*.cpp")

foreach(SOURCE_FILE ${TEST_SOURCES})
    # 读取文件路径，变成test target的名字: "simd/SSE2.cpp" -> "test_simd_SSE2"
    string(REPLACE "/" "_" TEST_TARGET_NAME "${SOURCE_FILE}")
    get_filename_component(TEST_TARGET_NAME ${TEST_TARGET_NAME} NAME_WE)

    add_tsimd_test_target(${TEST_TARGET_NAME} ${SOURCE_FILE})
endforeach()
